
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если ОсновныеНачисления.Количество() = 0 И ДополнительныеНачисления.Количество() = 0 Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Табличные части документа пустые, документ не может быть проведен.";
		Сообщение.Сообщить();
		Возврат
	КонецЕсли;
	
	
	ДвижениеПоВКМ_ОсновныеНачисления();
	СформироватьСторноЗаписи();
	СформироватьДвиженияПоПремииПроцентом();
	СформироватьНДФЛ();
	РасчитатьОклады();		
	РассчитатьОтпускныеНачисления();
	РасчитатьНДФЛ();
	СформироватьВзаиморасчетыССотрудниками(); 
	СформироваДвиженияВыполненныеСотрудникомРаботы(Отказ);
	

КонецПроцедуры  
#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

Процедура ДвижениеПоВКМ_ОсновныеНачисления() 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ ВТ_СписокСотрудников
		|ИЗ
		|	Документ.ВКМ_НачислениеЗаработнойПлаты.ОсновныеНачисления КАК ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления
		|ГДЕ
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.Ссылка = &Ссылка
		|	И ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ВидРасчета = &Оклад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_СписокСотрудников.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(ВКМ_УсловияОплатыСотрудниковСрезПоследних.Оклад, 0) КАК Оклад
		|ПОМЕСТИТЬ ВТ_Оклады
		|ИЗ
		|	ВТ_СписокСотрудников КАК ВТ_СписокСотрудников
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&ДатаДокумента, Сотрудник В
		|			(ВЫБРАТЬ
		|				ВКМ_НачислениеЗаработнойПлатыОсновынеНачисления.Сотрудник КАК Сотрудник
		|			ИЗ
		|				Документ.ВКМ_НачислениеЗаработнойПлаты.ОсновныеНачисления КАК ВКМ_НачислениеЗаработнойПлатыОсновынеНачисления
		|			ГДЕ
		|				ВКМ_НачислениеЗаработнойПлатыОсновынеНачисления.Ссылка = &Ссылка
		|				И ВКМ_НачислениеЗаработнойПлатыОсновынеНачисления.ВидРасчета = &Оклад)) КАК
		|			ВКМ_УсловияОплатыСотрудниковСрезПоследних
		|		ПО ВТ_СписокСотрудников.Сотрудник = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ВидРасчета КАК ВидРасчета,
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ДатаНачала КАК ДатаНачала,
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ДатаОкончания КАК ДатаОкончания,
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ГрафикРаботы КАК ГрафикРаботы,
		|	ВЫБОР
		|		КОГДА
		|			ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск)
		|			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ДатаНачала, МЕСЯЦ, -12), МЕСЯЦ)
		|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		|	КОНЕЦ КАК БазовыйПериодНачало,
		|	ВЫБОР
		|		КОГДА
		|			ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск)
		|			ТОГДА КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ДатаНачала, МЕСЯЦ, -1), МЕСЯЦ)
		|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		|	КОНЕЦ КАК БазовыйПериодКонец,
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.Ссылка.Дата КАК ПериодРегистрации,
		|	ЕСТЬNULL(ВТ_Оклады.Оклад, 0) КАК Показатель
		|ИЗ
		|	Документ.ВКМ_НачислениеЗаработнойПлаты.ОсновныеНачисления КАК ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Оклады КАК ВТ_Оклады
		|		ПО ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.Сотрудник = ВТ_Оклады.Сотрудник
		|ГДЕ
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	Запрос.УстановитьПараметр("ДатаДокумента",(Дата));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
Движения.ВКМ_ОсновныеНачисления.Очистить();
	Движения.ВКМ_ОсновныеНачисления.Записывать = Истина; 

	// регистр BKM_ОсновныеНачисления 	
	Пока Выборка.Следующий() Цикл 
		
		// Пропустить записи с нулевым или несуществующимим окладами
		Если Выборка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад
			И Выборка.Показатель = 0 Тогда
			
			Продолжить;
	
		КонецЕсли; 
		
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить(); 
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		Движение.ПериодДействияНачало = Выборка.ДатаНачала;
		Движение.ПериодДействияКонец = Выборка.ДатаОкончания;
		Движение.ГрафикРабот = Выборка.ГрафикРаботы;
	
	КонецЦикла;   
		
Движения.ВКМ_ОсновныеНачисления.Записать();
		
КонецПроцедуры  

Процедура СформироватьСторноЗаписи()
      
      СторноЗаписи = Движения.ВКМ_ОсновныеНачисления.ПолучитьДополнение();
      
	  Если Не ЗначениеЗаполнено(СторноЗаписи) Тогда
	      Возврат;
	  КонецЕсли; 
	  
	  Для Каждого Запись Из СторноЗаписи Цикл
	      
	      // Движения по регистру ВКМ_ОсновныеНачисления
	      Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
		  
	      ЗаполнитьЗначенияСвойств(Движение, Запись);
	      Движение.Сторно = Истина;
	      Движение.ПериодРегистрации = Дата;
	      Движение.ПериодДействияНачало = Запись.ПериодДействияНачалоСторно;
	      //@skip-check object-not-accessible-concrete-compatiblity-mode
	      //Ругается. Но если закомментировать неправильно считает сторно. (До конца месяца)
	      Движение.ПериодДействияКонец = Запись.ПериодДействияКонецСторно;
		        	  
	  КонецЦикла;
	  
	Движения.ВКМ_ОсновныеНачисления.Записать();
	
КонецПроцедуры

Процедура СформироватьДвиженияПоПремииПроцентом()	
	
	// регистр BKM_ДополнительныеНачисления	                   
	
	Движения.ВКМ_ДополнительныеНачисления.Записывать = Истина;
	
	Для Каждого ТекСтрокаДополнительныеНачисления Из ДополнительныеНачисления Цикл
		
		Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.ПремияПроцентом;
		Движение.ПериодРегистрации = Дата;
		Движение.Сотрудник = ТекСтрокаДополнительныеНачисления.Сотрудник;
		Движение.Результат = ТекСтрокаДополнительныеНачисления.СуммаНачисления;
		
	КонецЦикла;
	
	Движения.ВКМ_ДополнительныеНачисления.Записать();
		
КонецПроцедуры
Процедура СформироватьНДФЛ()
	
	СписокСотрудников = ОсновныеНачисления.Выгрузить(,"Сотрудник"); 
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДополнительныеНачисления,СписокСотрудников); 
	СписокСотрудников.Свернуть("Сотрудник"); 
	
	// регистр BKM_Удержания 
	Движения.ВКМ_Удержания.Записывать = Истина;
	
	Для Каждого Сотрудник Из СписокСотрудников  Цикл
		
		Движение = Движения.ВКМ_Удержания.Добавить(); 
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
		Движение.ПериодРегистрации = Дата;        
		Движение.БазовыйПериодНачало = НачалоМесяца(Дата);
		Движение.БазовыйПериодКонец = КонецМесяца(Дата);
		Движение.Сотрудник = Сотрудник.Сотрудник;
		
	КонецЦикла;
	
	Движения.ВКМ_Удержания.Записать();	
	
КонецПроцедуры

Процедура РасчитатьОклады() 
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия, 0) КАК Норма,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия, 0) КАК Факт
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(Регистратор = &Регистратор
		|	И ВидРасчета = &ВидРасчета) КАК ВКМ_ОсновныеНачисленияДанныеГрафика";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	
	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		
		// Движения по регистру ВКМ_ОсновныеНачисления
		Движение = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки - 1];
		Движение.ОтработаноДней = Выборка.Факт;
		Если Выборка.Норма <> 0 Тогда
			Движение.Результат = Движение.Показатель * Выборка.Факт / Выборка.Норма;
		КонецЕсли;	
		
		
		
		Если Движение.Сторно Тогда 
			
			Движение.Результат = -Движение.Результат;
			Движение.ОтработаноДней = -Движение.ОтработаноДней;
			
		КонецЕсли;
		
		
	КонецЦикла; 
		
	Движения.ВКМ_ОсновныеНачисления.Записать(, истина);
	
КонецПроцедуры

Процедура РассчитатьОтпускныеНачисления()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия, 0) КАК Факт,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.РезультатБаза, 0) +
		|		ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.РезультатБаза, 0) КАК РезультатБаза,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ОтработаноДнейБаза, 0) КАК ОтработаноДнейБаза
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ОсновныеНачисления(&Измерения, &Измерения,,
		|			Регистратор = &Регистратор
		|		И ВидРасчета = &Отпуск) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления
		|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ДополнительныеНачисления(&Измерения, &Измерения,,
		|			Регистратор = &Регистратор
		|		И ВидРасчета = &Отпуск) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления
		|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(Регистратор = &Регистратор
		|		И ВидРасчета = &Отпуск) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
		|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки
		|ГДЕ
		|	ВКМ_ОсновныеНачисления.Регистратор = &Регистратор
		|	И ВКМ_ОсновныеНачисления.ВидРасчета = &Отпуск";
	
	Запрос.УстановитьПараметр("Отпуск", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);
	Измерения = Новый Массив; 
	Измерения.Добавить("Сотрудник");
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
		
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки -1];
	
	
	
		Если Выборка.ОтработаноДнейБаза <> 0 Тогда
			Движение.Результат = Выборка.РезультатБаза * Выборка.Факт / Выборка.ОтработаноДнейБаза ;
		КонецЕсли;		
		Движение.ОтработаноДней = Выборка.Факт;
		
		Если Движение.Сторно Тогда 
			
			Движение.Результат = -Движение.Результат; 
			Движение.ОтработаноДней = -Выборка.Факт;
		КонецЕсли;
			
	КонецЦикла;
	
	//Движения.ВКМ_ОсновныеНачисления.Записать(,Истина); 
	
КонецПроцедуры

Процедура РасчитатьНДФЛ()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.НомерСтроки, 0) КАК НомерСтроки,
		|	ЕСТЬNULL(ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.РезультатБаза, 0) КАК РезультатБазаОсновныеНачисления
		|ПОМЕСТИТЬ ВТ_ОсновныеНачисления
		|ИЗ
		|	РегистрРасчета.ВКМ_Удержания.БазаВКМ_ОсновныеНачисления(&Измерения, &Измерения, &Разрезы, Регистратор = &Ссылка
		|	И ВидРасчета = &НДФЛ) КАК ВКМ_УдержанияБазаВКМ_ОсновныеНачисления
		|ГДЕ
		|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.Регистратор = ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.НомерСтроки, 0) КАК НомерСтроки,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.РезультатБаза, 0) КАК
		|		РезультатБазаДополнительныеНачисления
		|ПОМЕСТИТЬ ВТ_ДополнительныеНачисления
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ДополнительныеНачисления(&Измерения, &Измерения, &Разрезы,
		|		Регистратор = &Ссылка
		|	И ВидРасчета = &НДФЛ) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления
		|ГДЕ
		|	ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.Регистратор = ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.РегистраторРазрез
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВКМ_Удержания.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВТ_ДополнительныеНачисления.РезультатБазаДополнительныеНачисления, 0) +
		|		ЕСТЬNULL(ВТ_ОсновныеНачисления.РезультатБазаОсновныеНачисления, 0) КАК БазаНДФЛ
		|ИЗ
		|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОсновныеНачисления КАК ВТ_ОсновныеНачисления
		|		ПО ВКМ_Удержания.НомерСтроки = ВТ_ОсновныеНачисления.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДополнительныеНачисления КАК ВТ_ДополнительныеНачисления
		|		ПО ВКМ_Удержания.НомерСтроки = ВТ_ДополнительныеНачисления.НомерСтроки
		|ГДЕ
		|	ВКМ_Удержания.Регистратор = &Ссылка
		|	И ВКМ_Удержания.ВидРасчета = &НДФЛ"; 
	
	
	Запрос.УстановитьПараметр("НДФЛ", ПланыВидовРасчета.ВКМ_Удержания.НДФЛ);   
	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	Измерения = Новый Массив; 
	Измерения.Добавить("Сотрудник");                                 
	
	Разрезы = Новый Массив; 
	Разрезы.Добавить("Регистратор");

	Запрос.УстановитьПараметр("Разрезы", Разрезы);
	Запрос.УстановитьПараметр("Измерения", Измерения);
	
	
	Выборка = Запрос.Выполнить().Выбрать();
		
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_Удержания[Выборка.НомерСтроки - 1];
		Движение.Результат = Выборка.БазаНДФЛ * 13 / 100;
		
	КонецЦикла;	

	Движения.ВКМ_Удержания.Записать(,Истина);
	
КонецПроцедуры

Процедура СформироватьВзаиморасчетыССотрудниками()

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисления.Сотрудник КАК Сотрудник,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисления.Результат, 0) КАК Сумма
	|ПОМЕСТИТЬ ВТ_Результаты
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
	|ГДЕ
	|	ВКМ_ОсновныеНачисления.Регистратор = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВКМ_ДополнительныеНачисления.Сотрудник,
	|	ЕСТЬNULL(ВКМ_ДополнительныеНачисления.Результат, 0)
	|ИЗ
	|	РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
	|ГДЕ
	|	ВКМ_ДополнительныеНачисления.Регистратор = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВКМ_Удержания.Сотрудник,
	|	ЕСТЬNULL(ВКМ_Удержания.Результат, 0)
	|ИЗ
	|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
	|ГДЕ
	|	ВКМ_Удержания.Регистратор = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Результаты.Сотрудник,
	|	СУММА(ВТ_Результаты.Сумма) КАК Сумма
	|ИЗ
	|	ВТ_Результаты КАК ВТ_Результаты
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Результаты.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	// регистр BKM_ВзаиморасчетыССотрудниками Приход	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.Сумма = Выборка.Сумма;
	
	КонецЦикла;
	
КонецПроцедуры 

Процедура СформироваДвиженияВыполненныеСотрудникомРаботы(Отказ)

	Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;
	Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записать();;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = ДополнительныеНачисления;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Сотрудник", "Сотрудник");
	
	Попытка
		Блокировка.Заблокировать();	
	Исключение
		ОбщегоНазначения.СообщитьПользователю("Ошибка блокировки данных. Документ не проведен.",,,,Отказ);
	КонецПопытки;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_НачислениеЗаработнойПлатыДополнительныеНачисления.Сотрудник КАК Сотрудник,
		|	СУММА(ВКМ_НачислениеЗаработнойПлатыДополнительныеНачисления.СуммаНачисления) КАК СуммаКОплате,
		|	СУММА(ВКМ_НачислениеЗаработнойПлатыДополнительныеНачисления.НачисленоЧасов) КАК НачисленоЧасов
		|ПОМЕСТИТЬ ВТ_Сотрудник
		|ИЗ
		|	Документ.ВКМ_НачислениеЗаработнойПлаты.ДополнительныеНачисления КАК
		|		ВКМ_НачислениеЗаработнойПлатыДополнительныеНачисления
		|ГДЕ
		|	ВКМ_НачислениеЗаработнойПлатыДополнительныеНачисления.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_НачислениеЗаработнойПлатыДополнительныеНачисления.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Сотрудник.Сотрудник КАК Сотрудник,
		|	ВТ_Сотрудник.НачисленоЧасов КАК НачисленоЧасов,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ВТ_Сотрудник.Сотрудник) КАК ПредставлениеСотрудник,
		|	ЕСТЬNULL(ВКМ_ВыполненныеСотрудникомРаботыОстатки.ЧасовКОплатеОстаток, 0) КАК ЧасовКОплатеОстаток,
		|	ЕСТЬNULL(ВКМ_ВыполненныеСотрудникомРаботыОстатки.СуммаКОплатеОстаток, 0) КАК СуммаКОплатеОстаток,
		|	ВТ_Сотрудник.СуммаКОплате КАК СуммаКОплате
		|ИЗ
		|	ВТ_Сотрудник КАК ВТ_Сотрудник
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Остатки КАК
		|			ВКМ_ВыполненныеСотрудникомРаботыОстатки
		|		ПО ВТ_Сотрудник.Сотрудник = ВКМ_ВыполненныеСотрудникомРаботыОстатки.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	Выборка = Запрос.Выполнить().Выбрать();
	

	
	Пока  Выборка.Следующий() Цикл
		
		Если Выборка.СуммаКОплате > Выборка.СуммаКОплатеОстаток Тогда
			
			ТекстСообщения = СтрШаблон("Документ не проведен. Для сотрудника %1 сумма начисления (%2) больше чем сумма за выполненные работы (%3)",
			Выборка.ПредставлениеСотрудник,Выборка.СуммаКОплате, Выборка.СуммаКОплатеОстаток);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ); 
			
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		//Движения по регистру BKM_ВыполненныеСотрудникомРаботы 
		Движение = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.СуммаКОплате = Выборка.СуммаКОплате;
		
		Если  Выборка.СуммаКОплате =  Выборка.СуммаКОплатеОстаток Тогда
			Движение.ЧасовКОплате = Выборка.ЧасовКОплатеОстаток;
		Иначе				 
			Движение.ЧасовКОплате = Выборка.НачисленоЧасов;
		Конецесли;
				
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли