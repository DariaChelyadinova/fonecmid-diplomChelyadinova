

#Область ОбработчикиКомандФормы
&НаКлиенте
Асинх Процедура ЗаполнитьСотрудникми(Команда)
	Если Объект.Выплаты.Количество() <> 0 Тогда
		
		Результат = Ждать ВопросАсинх("Табличная часть - Выплаты будет очищена. Продолжить?", 
		РежимДиалогаВопрос.ДаНет,,,"Вопрос.");	
		
		Если Результат = Неопределено Или Результат = КодВозвратаДиалога.Нет Тогда				
			Возврат;			
		КонецЕсли; 
		
	КонецЕсли;                
	
	Объект.Выплаты.Очистить();	
	ЗаполнитьСотрудникмиНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСотрудникмиНаСервере()
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(ВКМ_ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток, 0) КАК Сумма,
		|	ВКМ_ВзаиморасчетыССотрудникамиОстатки.Сотрудник КАК Сотрудник
		|ИЗ
		|	РегистрНакопления.ВКМ_ВзаиморасчетыССотрудниками.Остатки(&ДатаДокумента,) КАК ВКМ_ВзаиморасчетыССотрудникамиОстатки
		|ГДЕ
		|	ВКМ_ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток > 0";
	
	
	Запрос.УстановитьПараметр("ДатаДокумента", Объект.Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ТекстСообщения = "Нет данных для формирования зарплатной ведомости.";
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
		
	Пока Выборка.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(Объект.Выплаты.Добавить(), Выборка);
		
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти